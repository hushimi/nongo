// ------------------------------------------------------------
// Gradle buildscript (for Flyway only)
// ------------------------------------------------------------
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.8.2'
    }
}

// ------------------------------------------------------------
// Gradle core plugins
// ------------------------------------------------------------
plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.7'
    id 'org.flywaydb.flyway' version '11.14.0'
}

// ------------------------------------------------------------
// Project info
// ------------------------------------------------------------
group = 'bushigen'
version = '0.0.1-SNAPSHOT'
description = 'Bushigen project'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

// ------------------------------------------------------------
// Repositories
// ------------------------------------------------------------
repositories {
    mavenCentral()
}

// ------------------------------------------------------------
// Configurations
// ------------------------------------------------------------
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    myBatisGen
}

// ------------------------------------------------------------
// Dependencies
// ------------------------------------------------------------
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:4.4.0'
    implementation 'org.springframework.security:spring-security-crypto'

    // OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    // DAO libs
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    implementation 'org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.5.0'
    implementation 'org.flywaydb:flyway-core:11.8.2'
    implementation 'org.flywaydb:flyway-mysql:11.8.2'
    implementation 'com.mysql:mysql-connector-j:9.4.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MyBatis Generator
    myBatisGen 'org.mybatis.generator:mybatis-generator-core:1.4.2'
    myBatisGen 'com.mysql:mysql-connector-j:9.4.0'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Dev & Test
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// ------------------------------------------------------------
// Flyway configuration
// ------------------------------------------------------------
flyway {
    url = 'jdbc:mysql://localhost:3306/nongodb'
    user = project.findProperty('db.user')
    password = project.findProperty('db.password')
    locations = ['classpath:db/migration']
    baselineOnMigrate = true
    baselineVersion = '0'
    baselineDescription = 'Initial baseline'
    cleanDisabled = false
}

// ------------------------------------------------------------
// MyBatis Generator task (JavaExec)
// ------------------------------------------------------------
task mybatisGenerate(type: JavaExec) {
    mainClass.set("org.mybatis.generator.api.ShellRunner")
    classpath = configurations.myBatisGen
    args = ["-configfile", "src/main/resources/generatorConfig.xml", "-overwrite"]
}

// ------------------------------------------------------------
// Svelte build integration
// ------------------------------------------------------------
task buildSvelteFront(type: Exec) {
    workingDir = file('src/svelte-front')
    commandLine 'npm.cmd', 'run', 'build'
}

bootRun.dependsOn buildSvelteFront
build.dependsOn buildSvelteFront

tasks.named('test') {
    useJUnitPlatform()
}
