// gradle内で使用するプラグイン(CorePlugin/GradlePluginPortal以外)
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.8.2'
    }
}

// gradle内で使用するプラグイン
plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.6'
    id 'org.flywaydb.flyway' version '11.14.0'
}

group = 'bushigen'
version = '0.0.1-SNAPSHOT'
description = 'Bushigen project'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// アプリ内で使用するプラグイン
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

    implementation 'org.flywaydb:flyway-core:11.8.2'
    implementation 'org.flywaydb:flyway-mysql:11.8.2'
    implementation 'com.mysql:mysql-connector-j:9.4.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Flyway configuration
flyway {
    url = 'jdbc:mysql://localhost:3306/nongodb'
    user = project.findProperty('db.user')
    password = project.findProperty('db.password')
    locations = ['classpath:db/migration']
    baselineOnMigrate = true
    baselineVersion = '0'
    baselineDescription = 'Initial baseline'
    cleanDisabled = false
}

// Svelte build integration
task buildSvelteFront(type: Exec) {
    workingDir = file('src/svelte-front')
    commandLine 'npm.cmd', 'run', 'build'
}
bootRun.dependsOn buildSvelteFront
build.dependsOn buildSvelteFront

tasks.named('test') {
    useJUnitPlatform()
}